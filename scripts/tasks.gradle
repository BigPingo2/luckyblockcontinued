def luckyConfigDir = "$projectDir/lucky_config"
def luckyRunConfigDir = "$projectDir/run/config/lucky/version-${project.version}"
def luckyAddonDir = "$projectDir/template_addon"
def luckyRunAddonDir = "$projectDir/run/addons/lucky/lucky_block_custom"
def tmpDir = "$buildDir/libs"
def distDir = "$buildDir/dist"

task luckyCopyConfig(type: Copy) {
    group = 'Lucky Block'
    description = 'Copies the Lucky Block configuration files to the run directory.'
    from luckyConfigDir
    into luckyRunConfigDir
}

task luckyCopyTemplateAddon(type: Copy) {
    group = 'Lucky Block'
    description = 'Copies the Lucky Block template add-on to the run directory.'
    from luckyAddonDir
    into luckyRunAddonDir
}

task luckyZipConfig(type: Zip) {
    group = 'Lucky Block'
    description = 'Creates a Lucky Block configuration zip archive.'
    destinationDir = file(tmpDir)
    archiveName = 'default_config.zip'
    from luckyConfigDir
}

task luckyPackage(type: Zip) {
    group = 'Lucky Block'
    description = 'Combines the build archive with the Lucky Block configuration files and generates the final zip.'
    destinationDir = file(distDir)
    archiveName = "${project.JAR_NAME_PRD}.jar"
    from zipTree("$tmpDir/${project.JAR_NAME_DEV}.jar")
    from("$tmpDir/default_config.zip") {
        into ('mod/lucky')
    }
}

task luckyClient {
    group = 'Lucky Block'
    description = 'Runs a Minecraft client with the Lucky Block.'
    dependsOn luckyCopyConfig
    dependsOn luckyCopyTemplateAddon
    dependsOn 'runClient'
}
task luckyServer {
    group = 'Lucky Block'
    description = 'Runs a Minecraft server with the Lucky Block.'
    dependsOn luckyCopyConfig
    dependsOn luckyCopyTemplateAddon
    dependsOn 'runServer'
}

task luckyBuild {
    group = 'Lucky Block'
    description = 'Builds the Lucky Block.'
    dependsOn build
    dependsOn luckyZipConfig
    dependsOn luckyPackage

    luckyZipConfig.shouldRunAfter(build)
    luckyPackage.shouldRunAfter(luckyZipConfig)
}
